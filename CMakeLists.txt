cmake_minimum_required(VERSION 3.24)

if(NOT DEFINED CMAKE_CUDA_ARCHITECTURES OR CMAKE_CUDA_ARCHITECTURES STREQUAL "")
    set(CMAKE_CUDA_ARCHITECTURES "70;75;80" CACHE STRING "CUDA architectures to build for" FORCE)
endif()

project(mxgpu_bench LANGUAGES CUDA CXX)

option(USE_CUDA_FALLBACK "Build with CUDA toolkit instead of hc SDK" OFF)
option(BUILD_HC_BENCHES "Build hc SDK benchmark executables" ON)
option(BUILD_NVIDIA_BENCHES "Build CUDA (NVIDIA) benchmark executables" OFF)
set(CMAKE_CUDA_STANDARD 14)
set(CMAKE_CUDA_STANDARD_REQUIRED ON)
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin)

add_library(benchmark_common INTERFACE)
target_include_directories(benchmark_common INTERFACE ${CMAKE_CURRENT_SOURCE_DIR}/include)
if(USE_CUDA_FALLBACK)
    target_compile_definitions(benchmark_common INTERFACE USE_CUDA_FALLBACK=1)
endif()

if(NOT BUILD_HC_BENCHES AND NOT BUILD_NVIDIA_BENCHES)
    message(FATAL_ERROR "Enable BUILD_HC_BENCHES and/or BUILD_NVIDIA_BENCHES to build something useful")
endif()

if(USE_CUDA_FALLBACK OR BUILD_NVIDIA_BENCHES)
    find_package(CUDAToolkit REQUIRED)
endif()

if(BUILD_HC_BENCHES)
    set(_runtime_lib "")
    set(_blas_lib "")
    if(USE_CUDA_FALLBACK)
        set(_runtime_lib CUDA::cudart)
        set(_blas_lib CUDA::cublas)
    else()
        set(HC_RUNTIME_LIB "" CACHE STRING "Path to hc runtime shared library")
        set(HCBLAS_LIB "" CACHE STRING "Path to hcBLAS shared library")
        if(HC_RUNTIME_LIB AND EXISTS ${HC_RUNTIME_LIB})
            set(_runtime_lib ${HC_RUNTIME_LIB})
        else()
            find_library(HC_RUNTIME_LIB_FALLBACK hc_runtime)
            if(HC_RUNTIME_LIB_FALLBACK)
                set(_runtime_lib ${HC_RUNTIME_LIB_FALLBACK})
            endif()
        endif()

        if(HCBLAS_LIB AND EXISTS ${HCBLAS_LIB})
            set(_blas_lib ${HCBLAS_LIB})
        else()
            find_library(HCBLAS_LIB_FALLBACK hcblas)
            if(HCBLAS_LIB_FALLBACK)
                set(_blas_lib ${HCBLAS_LIB_FALLBACK})
            endif()
        endif()

        if(NOT _runtime_lib OR NOT _blas_lib)
            message(FATAL_ERROR "Set HC_RUNTIME_LIB and HCBLAS_LIB or build with -DUSE_CUDA_FALLBACK=ON or disable BUILD_HC_BENCHES")
        endif()
    endif()

    add_executable(fp64_flops src/fp64_flops.cu)
    target_link_libraries(fp64_flops PRIVATE benchmark_common ${_runtime_lib})

    add_executable(memory_bandwidth src/memory_bandwidth.cu)
    target_link_libraries(memory_bandwidth PRIVATE benchmark_common ${_runtime_lib})

    add_executable(hcblas_gemm src/hcblas_gemm.cu)
    target_link_libraries(hcblas_gemm PRIVATE benchmark_common ${_runtime_lib} ${_blas_lib})
endif()

if(BUILD_NVIDIA_BENCHES)
    add_executable(cuda_fp64_flops src/cuda_fp64_flops.cu)
    target_include_directories(cuda_fp64_flops PRIVATE ${CMAKE_CURRENT_SOURCE_DIR}/include)
    target_link_libraries(cuda_fp64_flops PRIVATE CUDA::cudart)

    add_executable(cuda_memory_bandwidth src/cuda_memory_bandwidth.cu)
    target_include_directories(cuda_memory_bandwidth PRIVATE ${CMAKE_CURRENT_SOURCE_DIR}/include)
    target_link_libraries(cuda_memory_bandwidth PRIVATE CUDA::cudart)

    add_executable(cublas_gemm src/cublas_gemm.cu)
    target_include_directories(cublas_gemm PRIVATE ${CMAKE_CURRENT_SOURCE_DIR}/include)
    target_link_libraries(cublas_gemm PRIVATE CUDA::cudart CUDA::cublas)
endif()
